/**
 * Grammar created for translating a decision tree into
 * NodeJS code for creating an IBM Watson AssistantV1 chatbot.
 * 
 * This language's name is, albeit
 * not at all serious, WaltsonJS.
 * (credit to Jayshon Adams)
 * 
 * Created by Walt Loumakis
 */

program		:	definition
			|	definition program

definition	:	explicitDef
			|	implicitDef

explicitDef	:	ID COLON unary

implicitDef	:	unary

unary		:	ID
			|	TRUE
			|	FALSE
			|	number
			|	STRING
			|	SKIP
			|	boolExpr
			|	object
			|	array

number		:	nonnegNum
			|	negNum

nonnegNum	:	INTEGER
			|	REAL

negNum		:	UMINUS INTEGER
			|	UMINUS REAL

boolExpr	:	boolUnary
			|	boolUnary boolOp boolExpr
			|	OPAREN boolExpr CPAREN

boolUnary	:	intent
			|	entity
			|	context

boolOp		:	AND
			|	OR

intent		:	HASH ID

entity		:	AT ID

context		:	DOLLAR ID

object		:	OBRACE optProgram CBRACE

optProgram	:	program
			|	*EMPTY*

array		:	OBRACKET mixinList CBRACKET

mixinList	:	mixin
			|	mixin mixinList

mixin		:	definition
			|	object
