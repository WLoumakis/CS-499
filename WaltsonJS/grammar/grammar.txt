/**
 * Grammar created for translating a decision tree into
 * NodeJS code for creating an IBM Watson AssistantV1 chatbot.
 * 
 * This language's name is, albeit
 * not at all serious, WaltsonJS.
 * (credit to Jayshon Adams)
 * 
 * Created by Walt Loumakis
 */

program		:	definition
			|	definition program

definition	:	explicitDef
			|	implicitDef

explicitDef	:	ID COLON unary
			|	STRING COLON unary

implicitDef	:	unary

unary		:	TRUE
			|	FALSE
			|	STRING
			|	SKIP
			|	URL
			|	number
			|	boolExpr
			|	object
			|	array

number		:	nonnegNum
			|	negNum

nonnegNum	:	INTEGER
			|	REAL

negNum		:	UMINUS INTEGER
			|	UMINUS REAL

boolExpr	:	boolUnary
			|	boolUnary boolOp boolExpr
			|	OPAREN boolExpr CPAREN

boolUnary	:	intent
			|	entity
			|	context

boolOp		:	AND
			|	OR

intent		:	HASH ID

entity		:	AT ID

context		:	DOLLAR ID

object		:	OBRACE optAttrList CBRACE

optAttrList	:	attrList
			|	*EMPTY*

attrList	:	definition
			|	definition attrList

array		:	OBRACKET optMixinList CBRACKET

optMixinList:	mixinList
			|	*EMPTY*

mixinList	:	mixin
			|	mixin mixinList

mixin		:	definition
			|	object
